parserImport XOCL;

/******************************************************************************
 *                                                                            *
 *                              Identifier Tables                             *
 *                            ----------------------                          *
 *                                                                            *
 *    XML is tree shaped. To support graphs and therefore sharing XML uses    *
 *    ids allocated to elements. Multiple occurrences of the same element     *
 *    reference the same id. When generating XML from XMF elements, the ids   *
 *    are automatically allocated and the referencing is managed via the      *
 *    idtable which is supplied to the initial call of the toXML operation.   *
 *                                                                            *
 ******************************************************************************/

import XML;

context Generator

  @Class IdTable
  
    @Attribute table   : Table   end
    @Attribute counter : Integer end
    
    @Constructor()
      self.table := Table(100)
    end
    
    @Operation allocateId(value)
      table.put(value,counter);
      self.counter := counter + 1;
      table.get(value)
    end
    
    @Operation hasId(value):Boolean
      table.hasKey(value)
    end
    
    @Operation idAttribute(value,name)
      Attribute(name,self.ref(value))
    end
    
    @Operation ref(value)
      if table.hasKey(value)
      then table.get(value)
      else self.allocateId(value)
      end
    end
    
  end