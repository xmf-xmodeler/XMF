parserImport XOCL;

/******************************************************************************
 *                                                                            *
 *                                 Packages                                   *
 *                              ---------------                               *
 *                                                                            *
 *   Packages are both name-spaces and classes. This allows packages to have  *
 *   meta-packages whose contents should be meta-classes of the corresponding *
 *   contents of the instance-package.                                        *
 *                                                                            *
 *   For the purposes of normal usage, you can treat packages simply as       *
 *   ordinary name-spaces. In this regard, packages only have special status  *
 *   because they have concrete syntax support in terms of @Package P ... end *
 *                                                                            *
 *   Unlike basic name-spaces, Packages know about certain types of named-    *
 *   elements that they contain, for example classes, operations and packages.*
 *                                                                            *
 *   Unlike name-spaces or classes, when an operation is added to a package   *
 *   the package is implicitly imported into the operation. This allows       *
 *   operations in the same package to call each other without using qualified*
 *   names (e.g. P::o()).                                                     *
 *                                                                            *
 *   Packages have imports. This feature is not used.                         *
 *                                                                            *
 *   You can use packages as classes by instantiating them. This feature is   *
 *   not really encouraged - use classes if you want instantiation. Packages  *
 *   should be viewed as name-spaces with some extra features. Particularly   *
 *   the implicit import of the package into the package-operations.          *
 *                                                                            *
 ******************************************************************************/ 

context Package
  // Use 'add' to add either an operation or a classifier 
  // to a package.
  @Operation add(n:NamedElement):Classifier
    if n.isKindOf(Operation)
    then self.addOperation(n); super(n)
    else 
      if n.isKindOf(Package)
      then
        self.addPackage(n);
        super(n)
      elseif n.isKindOf(Class)
      then
        self.addClass(n);
        super(n)
      else
        super(n)
      end
    end
  end
  
context Package 
  // addClass is used to add classes to a package.
  @Operation addClass(c:Class)
    self.resolveNameClash(c.name,classes);
    c.setOwner(self);
    self.classes := classes->including(c);
    self
  end
  
context Package
  @Operation addOperation(op)
  
    // When an operation is added to a package the package is automatically
    // imported into the name space. This should be moved to Classifier if
    // we want the same to happen to classes.
    
    op.importNameSpace(self);
    super(op)
  end
  
context Package
  // addPackage is used to add a sub-package to a package.
  @Operation addPackage(p:Package)
    self.resolveNameClash(p.name,packages);
    p.setOwner(self);
    self.packages := packages->including(p);
    self
  end
  
context Package
  @Operation allContents():Set(Classifier)
    @Doc
      Packages are containers with parents and therefore inherit
      the contents of their parents.
    end
    parents->iterate(parent allContents = self.contents() | 
      allContents + parent.allContents())
  end
  
context Package
  @Operation allClasses():Set(XCore::Class)
    self.allContents()->select(c | c <> null and c.isKindOf(XCore::Class)) 
  end
  
context Package
  @Operation allPackages():Set(XCore::Package)
    self.allContents()->select(c | c <> null and c.isKindOf(XCore::Package))
  end

context Package
  @Operation availableContents():Set(Classifier)
    @Doc
      This is the union of allContents and importedContents.
    end
    self.allContents() + self.importedContents()
  end

context Package
  @Operation availableContents(type:Classifier):Set(Classifier)
    self.availableContents()->select(c | c <> null andthen c.of() = type)
  end
  
context Package
  @Operation checkConstraints():ConstraintReport
    @Doc
      A package is an object and is checked against its classifier in
      the normal way. In addition, a package has a meta-package that
      classifies its elements. Both of these classifications are
      performed and then merged.
    end
    let selfClassification:ConstraintReport = super();
        metaClassification:ConstraintReport = self.checkMetaConstraints() then
        satisfied = selfClassification.satisfied() and metaClassification.satisfied();
        children = selfClassification.children + metaClassification.children
    in ConstraintReport(self,null,satisfied,"",children)
    end
  end
  
context Package
  @Operation checkMetaConstraints():ConstraintReport
  
    @Doc
      Classify the elements of the package with respect to the meta-package. The
      constraints of meta-packages are inherited through both the parents and the
      meta-meta-package etc.
    end
    
    @Letrec
      classify(metaPackage:Package,prev:Package,done:{Constraint},level:Integer):{ConstraintReport} = 
        let C:{Constraint} = metaPackage.allConstraints() then
            reports:{ConstraintReport} = runConstraints(C->reject(c | done->contains(c)),level)
        in prev := metaPackage;
           metaPackage := metaPackage.metaPackage;
           if prev = metaPackage
           then reports
           else reports + classify(metaPackage,prev,done+C,level+1)
           end
        end;
      applyConstraint(c:Constraint,o:Object,level:Integer):{ConstraintReport} =
        @TypeCase(o)
          Package do
            {doConstraint(c,o,level)} + o.classes->collect(class | applyConstraint(c,class,level)).flatten()
          end
          Class do
            {doConstraint(c,o,level)} + o.attributes->collect(att | applyConstraint(c,att,level)).flatten()
          end
          Attribute do
            {doConstraint(c,o,level)}
          end
          else {}
        end;
      doConstraint(c:Constraint,o:Object,level:Integer):ConstraintReport =
        try c(o,c.owner,level)
        catch(exception)
          ConstraintReport(self,c,false,"Error while performing classification: " + exception.message)
        end;
      runConstraints(C:{Constraint},level:Integer):{ConstraintReport} =
        let reports:{ConstraintReport} = {}
        in @While not C->isEmpty do
             let c = C->sel then
                 R:{ConstraintReport} = applyConstraint(c,self,level)
             in reports := reports + R;
                C := C->excluding(c)
             end
          end; 
          reports
        end
    in let reports:{ConstraintReport} = classify(metaPackage,metaPackage,{},1)
       in ConstraintReport(self,null,reports->forAll(r | r.satisfied()),"",reports->asSeq)
       end
    end
  end

context Package
  @Operation defaultParents():Set(Classifier)
    Set{Object}
  end 
  
context Package
  @Operation dependencies():Set(Element)
    super() +
    classes->iterate(c D = Set{} | D + c.dependencies()) +
    packages->iterate(p D = Set{} | D + p.dependencies())
  end

context XCore::Package
  @Operation importedContents():Set(Classifier)
    imports->iterate(i importedContents = Set{} | 
      importedContents + i.contents()->select(i |
        i <> null and i <> self and i.isKindOf(Classifier))
      )
  end

context Package
  // To initialise a package, initialise the contents and run super.
  @Operation init():Classifier
    if metaPackage = null 
    then self.metaPackage := XCore
    end;
    self.initOperations();
    self.initParents();
    super()
  end
  
context Package
  @Operation isMetaPackage():Boolean
    self.inheritsFrom(XCore)
  end
  
context Package
  @Operation metaPackage():Package
    if metaPackage = null
    then XCore
    else metaPackage
    end
  end

context Package

  // Remove an element from a package. Needs to be redefined
  // from the inherited NameSpace::remove because a package is 
  // a namespace for operations, packages and classes.
  
  @Operation remove(n:NamedElement):Classifier
    if n.isKindOf(Operation)
    then 
      self.removeOperation(n); 
      super(n)
    else
      if n.isKindOf(Package)
      then 
        self.removePackage(n);
        super(n)
      else 
        if n.isKindOf(Class)
        then 
          self.removeClass(n);
          super(n)
        else super(n)
        end
      end
    end
  end
  
context Package 
  @Operation removeClass(class)
    self.classes := classes->excluding(class)
  end
  
context Package
  @Operation removePackage(package)
    self.packages := packages->excluding(package)
  end
  
context Package
  @Operation repopulate(namedElement:NamedElement):Boolean
    if namedElement.isReallyKindOf(Package)
    then 
      super(namedElement);
      @For newClass in namedElement.classes do
        @Find(oldClass,classes)
          when oldClass.name() = newClass.name()
          do oldClass.repopulate(newClass)
          else self.add(newClass)
        end
      end;
      @For newPackage in namedElement.packages do
        @Find(oldPackage,packages)
          when oldPackage.name() = newPackage.name()
          do oldPackage.repopulate(newPackage)
          else self.add(newPackage)
        end
      end;
      true
    else false
    end
  end