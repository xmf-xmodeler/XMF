parserImport XOCL;

import Clients;
import Forms;
import IO;

context Forms

  @Class FormTextBox extends FormElement, TextComponent

    @Bind red   = "red"   end
    @Bind blue  = "blue"  end
    @Bind green = "green" end

    @Attribute wordRules : SeqOfElement (+) end
    @Attribute labelText : String end

    @Constructor(text,isEditable,labelText) end

    @Constructor(id,text,isEditable,labelText) end

    @Operation new(parent)
      self.commandInterpreter().newTextBoxCommand(parent,self);
      self.commandInterpreter().setText(self,self.text);
      self.newWordRules();
      self
    end

    @Operation addWordRule(word,colour)
      self.addToWordRules(Seq{word | colour});
      self.commandInterpreter().addTextBoxRule(self,word,colour)
    end

    @Operation changesMade(value:Boolean)
      self.commandInterpreter().changesMade(self,value)
    end

    @Operation doubleSelected()
      self
    end

    @Operation getText()
      self.commandInterpreter().getText(self)
    end

    @Operation newWordRules()
      @For rule in wordRules do
        let word = rule->head;
            colour = rule->tail
        in
          self.commandInterpreter().addTextBoxRule(self,word,colour)
        end
      end
    end

    @Operation renew()
      self.renew(null)
    end

    @Operation renew(parent)
      self.commandInterpreter().setText(self,self.text);
      self.newWordRules();
      self
    end

  end
