parserImport XOCL;

import Clients;
import Diagrams;
import ClassDiagrams;

context ClassDiagrams

  @Class OperationGroup extends Group
  
    @Attribute instLevel  : InstLevelText end
    @Attribute name  : OperationText end
    @Attribute metaclass_ : Text end
      
    @Operation initOperationGroup(nameArg,metaclass0,instLevel,x,y) // initOperationGroup
      self.name := OperationText(nameArg,x+16,y);
      if metaclass0 <> "" then
        self.metaclass_ := Text("<<" + metaclass0 + ">> ",x + 100,y,false)
      else
        self.metaclass_ := Text("",x + 120,y,false)
      end;      
      self.instLevel := InstLevelText(x,y);
      self.instLevel.setLevel(instLevel+"");
      // A group should not have any Display attributes set.
      if not "N"= (instLevel+"")
      then self.instLevel.setFillColor(Clients::Diagrams::Color(0,0,0))
      end;
      
      self.x := 0;
      self.y := 0
    
    end
    
    @Operation addDisplays()
      self.add(/*self.*/name);
      self.add(/*self.*/metaclass_);
      self.add(/*self.*/instLevel)
    end
    
    @Operation name()
      self.name.opName
    end

    @Operation moveDown()
      @Count i from 1 to 16 do
        name.move(name.x,name.y-1);
        metaclass_.move(metaclass_.x,metaclass_.y-1)
      end
    end
    
    @Operation resized(display)
    
      // If the nameLabel has resized then the type
      // may need to move along accordingly.
      
      if metaclass_.x() < (name.x() + name.width()) or
         metaclass_.x() > (name.x() + name.width() + self.separator())
      then 
        metaclass_.move(name.x() + name.width() + self.separator(),metaclass_.y());
        owner.resized(self)
      end
    end
    
    @Operation setName(name)
      self.name.setName(name)
    end
    
    @Operation setOperation(operation)
      self.name.setOperation(operation)
    end
    
    @Operation separator()
      // Between the name and the type...
      10
    end
  
  end