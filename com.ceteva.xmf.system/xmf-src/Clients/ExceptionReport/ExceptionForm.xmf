parserImport XOCL;

import Clients;
import Forms;
import ExceptionReport;

context ExceptionReport

  @Class ExceptionForm extends LockableFormWithHistory

    @Attribute button    : FormButton end
    @Attribute exception : Exception end
    @Attribute tree      : ReportTree end
    
    @Constructor(exception)
      self.typeName := "Exception";
      let msg = exception.message
      in
        self.itemName := msg
        //if msg.size()>75
        //then
        //  msg.truncate(75) + "..." //TODO: why do we truncate this? imho the client should decide this
        //else
        //  msg
        //end
      end;
      self.initialize()
/*      if exception.message = ""
      then self.name := exception.toString()
      else self.name := exception.message
      end */
    end
  
    @Operation addContents()
      // create elements

      self.tree := ReportTree(10,30,650,400,exception);

      //let button = FormButton(560,6,100,18,"View As Text",self.toTextEditor()) //constructor was broken, src: @Constructor(label,action) end
      self.button := FormButton("View As Text", self.toTextEditor());

      self.addElement(FormLabel(0,450," ")); //TODO: is this supposed to be padding?
      self.addElement(button);
      self.addElement(tree);
      self.addElement(FormLabel(10,9,"Exception Trace: " + itemName))
    end

    @Operation historyDialogMessage():String
      "Select an exception trace to view"
    end

    @Operation historyDialogTitle():String
      "Exception Trace History"
    end

    @Operation toTextEditor()
      @Operation()
        let editor = Clients::TextEditor::Editor(self,"Exception Trace",exception.message,false) 
        in 
          xmf.findClient(Clients::TextEditor::TextEditorClient).newTextEditor(editor);
          let out = IO::StringOutputChannel()
          in
            tree.pprint(out,0);
            editor.setText(out.getString())
          end
        end
      end
    end
    
  end