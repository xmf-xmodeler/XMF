parserImport XOCL;

import Signatures;
import OCL;
import Clients;
import TextEditor;

context TextEditor

  @Class VarDescriptor

    // A descriptor used by an EditorWalker to record variable information
    // during the walk. These are bound in an environment during a walk of 
    // the source code. Any feature of a var descriptor might be null if 
    // the information is not available.

    @Attribute type:Classifier                          end
    @Attribute definingLocation:Located                 end
    @Attribute tooltip:String   = ""                    end
    @Attribute actions:[Action] = []                    end

    @Constructor()                                      end
    @Constructor(type) !                                end
    @Constructor(type,definingLocation)                 end
    @Constructor(type,definingLocation,tooltip)         end
    @Constructor(type,definingLocation,tooltip,actions) end

    @Operation sendEditor(editor,charStart,charEnd)

      // Inform the editor of any information that should be displayed
      // about this variable...

      if tooltip <> "" then editor.setTooltip(tooltip,charStart,charEnd) end;
      @For action in actions do action.sendEditor(editor,charStart,charEnd) end;
      if type <> null then editor.varType(type,charStart,charEnd) end;
      if definingLocation <> null 
      then 
        let l:Located = definingLocation
        in editor.varDeclared(charStart,charEnd,l.charStart,l.charEnd)
        end
      end
    end

  end