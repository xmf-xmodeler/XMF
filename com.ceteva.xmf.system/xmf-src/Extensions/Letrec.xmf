parserImport Parser::BNF;
parserImport XOCL;

import OCL;
import XOCL;
import Parser::BNF;

context XOCL

  @Class Letrec extends Sugar
  
    @Doc
      A letrec construct allows bindings to be established for a local
      scope. The bindings are mutually recursive. Since XMF has eager
      evaluation, this means that the values of the bindings should be
      operations.
     
      Constructor: Letrec(bindings,body)
    end
  
    @Grammar extends OCL::OCL.grammar
    
      Letrec ::=  bindings = Bindings 'in' body = Exp 'end' { Letrec(bindings,body) }.
      
    end 
    
    @Attribute bindings : Seq(ValueBinding) end
    @Attribute body : Performable end
    
    @Constructor(bindings,body) end
    
    @Operation desugar() 
      let bindings = bindings->collect(binding | binding.desugar())
      in Let(bindings->collect(b | ValueBinding(b.name,[| null |])),
          [| @Operation()
               <Let(bindings->collect(b | 
                      if b.name.isKindOf(Drop)
                      then ValueBinding(Drop([| <b.name.exp> + "'" |]),b.value)
                      else ValueBinding(b.name + "'",b.value)
                      end),
                    bindings->iterate(b e = body | 
                     if b.name.isKindOf(Drop)
                     then [| let x = <Var(Drop([| <b.name.exp> + "'" |]))>
                             in <VarUpdate(Drop([| OCL::Var(<b.name.exp>) |]),[| x |])>; <e> 
                             end |]
                     else [| <VarUpdate(b.name,Var(b.name + "'"))>; <e> |]
                     end))>
             end()
          |])
      end
    end 
    
    @Operation pprint(out,indent)
      format(out,"@Letrec ~%~V",Seq{indent + 2});
      @For b in bindings do
        if not isFirst then format(out,";~%~V",Seq{indent+2}) end;
        b.pprint(out,indent)
      end;
      format(out,"~%~Vin ",Seq{indent});
      body.pprint(out,indent+2);
      format(out,"~%~Vend",Seq{indent})
    end
     
  end