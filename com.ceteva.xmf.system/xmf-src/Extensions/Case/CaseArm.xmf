parserImport XOCL;

/******************************************************************************
 *                                                                            *
 *                                Case Arms                                   *
 *                           -------------------                              *
 *                                                                            *
 *   A case-expression is supplied with a sequence of values and tries a      *
 *   sequence of case-arms in turn until one of the arms matches the values.  *
 *   A case-arm is a sequence of patterns (one for each of the values) and an *
 *   action. If the patterns match the corresponding values then the action is*
 *   performed after binding all the variables in the patterns. A pattern may *
 *   be non-deterministic and case-arm pattern-matching will perform back-    *
 *   tracking and try alternative selections until the alternatives are       *
 *   exhausted or the patterns all match.                                     *
 *                                                                            *
 ******************************************************************************/ 

import OCL;

context XOCL

  @Class CaseArm 
  
    // The sequence of patterns to try...
  
    @Attribute patterns : Seq(Pattern) end
    
    // The action to perform if the patterns are
    // successful...
    
    @Attribute action   : Performable  end
    
    @Constructor(patterns,action) ! end
    
    @Operation arity()
      patterns->size
    end
    
    @Operation desugar(vars,fail:Performable):Performable
    
      // Called by the case-expression to turn an arm into
      // pattern-matching code. The vars are bound to the
      // values to be matched against the patterns and the
      // fail expression is used if all alternatives are
      // exhausted...
      
      self.desugarPatterns(patterns,vars,fail)
    end
    
    @Operation desugarPatterns(patterns,vars,fail)
    
      // If any of the patterns involve choice then create a
      // new backtracking operation and try all the patterns...
      
      let isBacktracking = patterns->exists(pattern | pattern.needsBacktrack())
      in if isBacktracking
         then [| let backtrack = @Operation() <fail> end 
                 in <self.desugarAll(patterns,vars,[| backtrack() |])>
                 end 
              |]
         else 
           
           // No need to pass in a fail continuation since the
           // fail expression will be performed once at most...
           
           self.desugarAll(patterns,vars,fail)
         end
      end
    end
    
    @Operation desugarAll(patterns,vars,fail)
      if patterns->isEmpty
      then action
      else 
      
        // Ensure that any pattern variable is bound to the appropriate
        // value before the pattern matching code is generated. All patterns
        // are bound to a new variable. The 'newVar/0' operation will generate
        // a variable for a Varp with the same name. Other patterns just 
        // generate dummy variables...
        
        let pattern = patterns->head then
            newVarName = pattern.newVar() then
            newVar = Var(newVarName)
        in [| let <newVarName> = <vars->head>
              in <pattern.matchCode(newVar,self.desugarAll(patterns->tail,vars->tail,fail),fail)>
              end
           |]
        end
      end
    end
    
    @Operation desugarConst(vars,alt)
    
      // Turn into an if-expression...
      
      let tests = @For var,pattern in vars,patterns produce BinExp(var,"=",pattern.const) end then
          test = tests->tail->iterate(test conj = tests->head | BinExp(test,"and",conj))
      in If(test,action,alt)
      end
    end
    
    @Operation isConst():Boolean
    
      // Returns true when all the patterns are constants....
      
      patterns->forAll(pattern | pattern.isKindOf(Constp))
      
    end
    
    @Operation lift()
      let patternExps = SetExp("Seq",patterns->collect(p | p.lift()))
      in [| XOCL::CaseArm(<patternExps>,<action.lift()>) |]
      end
    end
    
    @Operation pprint(out,indent)
      @For pattern in patterns do
        pattern.pprint(out);
        if not isLast
        then format(out,",")
        end
      end;
      format(out," do~%~V",Seq{indent+2});
      action.pprint(out,indent+2);
      format(out,"~%~Vend",Seq{indent})
    end
    
  end 