parserImport XOCL;

import Exceptions;
import Compiler;
import OCL;
import XOCL; 

context Class
  @Operation compile(env,frame,isLast,saveSource)
  
    // Compilation of a single class definition is complicated because the
    // class might need to reference other classes in the same name-space.
    // To facilitate this, classifier references are delayed via closures.
    // The closures are removed when the class is initialized by its owner.
    
    self.compileClassPopulation(self.compileClassCreation(env,frame),env,frame,isLast,saveSource)
  end 

context Class
     
     @Operation compileClassCreation(env,frame)
     
       // Turn all type references in the parents of the class into
       // standard expressions delayed by an operation. Initialisation
       // of a compiled class will perform the operation and thereby
       // construct the parents. The delay is necessary to allow classes
       // to involve forward references in a package.
        
       let parents = 
             if parents->isEmpty 
             then [| @Operation <name.toString() + "DelayedDefaultParents">() newClass.defaultParents() end |] 
             else [| @Operation <name.toString() + "DelayedParents">() 
                       try <parents->iterate(p e = [| Set{} |] | [| <e> ->including(<p>) |])> 
                       catch(exception)
                         throw Exceptions::ClassInheritanceException(<name.toString().lift()>,<parents->map("pprint").lift()>,exception)
                       end
                     end |]
             end;
           nameExp = if name.isKindOf(Performable) then name else StrExp(name) end
       in [| let newClass = <self.metaExp()>.new() in
             let parents = <parents>
             in parents.owner := newClass;
                newClass.set("name",XCore::Symbol(<nameExp>));
                newClass.set("parents",parents);
                newClass.set("isAbstract",<BoolExp(isAbstract)>);
                newClass.set("imports",@Imports(imports) imports end);
                <self.initExp([| newClass |])>
             end end
          |].setLine(line).compile(env.incNameSpaceRefs(),frame,false,false)
       end
     end 

context Class
     
     @Operation compileClassPopulation(classInstrs,env,frame,isLast,saveSource)
       // Compile the elements and add them to the class at the head
       // of the stack.
       let env = env.incNameSpaceRefs().consNameSpaceRef(name.toString(),1);  // The class is defined wrt its container.
           addDelayed = Compiler::Symbol("addDelayed");
           // Wrap an operation round this so that we can bind 'self' via invoke...
           wrapOp(def) = [| @Operation() <def> end |]
       in self.allDefs()->iterate(def instrs = classInstrs |
            Seq{Instrs::StartCall() | wrapOp(def).compile(env,frame,false,saveSource)} + 
            instrs + 
            Seq{Instrs::Send(addDelayed,1)})
       end
     end 

context Class
     
     @Operation maxLocals()
       // A class definition *should* be Sugar. However, until this happens
       // calculate the max locals by hand.
       let importsLocals = [| @Imports(imports) imports end |].maxLocals();
           creationLocals = 2
       in defs->iterate(def maxLocals = importsLocals + creationLocals | maxLocals.max(def.maxLocals()))
       end
     end