parserImport XOCL;

/******************************************************************************
 *                                                                            *
 *                                File Input Channels                         *
 *                              -----------------------                       *
 *                                                                            *
 *    Files are read via file input channels. Create a channel by supplying   *
 *    the path to the file. An underlying VM channel is created to access the *
 *    file. Characters are successively read and returned as integer ASCII    *
 *    codes. The channel returns -1 on EOF.                                   *
 *                                                                            *
 ******************************************************************************/

import IO;

context IO

  @Class FileInputChannel extends InputChannel
        
    @Attribute fileName : String  end
    @Attribute channel  : Element end
    
    @Constructor(fileName) !
      self.channel := Kernel_fileInputChannel(fileName)
    end
    
    @Operation asString():String
      Kernel_readString(channel)
    end
    
    @Operation channel()
      channel
    end
        
    @Operation close()
      Kernel_close(channel)
    end
      
    @Operation eof():Boolean
      Kernel_eof(channel)  
    end 
    
    @Operation peek():Integer
      Kernel_peek(channel)
    end
       
    @Operation read():Integer
       Kernel_read(channel)
    end
    
    @Operation read(v:Vector):Integer
      Kernel_readVector(channel,v)
    end
    
    @Operation setChannel(channel)
      self.channel := channel
    end
     
  end